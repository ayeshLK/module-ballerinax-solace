/*
 * Copyright (c) 2025, WSO2 LLC. (http://www.wso2.com)
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs'
}

description = 'Ballerina - Solace Native'

configurations {
    dist {
        transitive true
    }
}

dependencies {
    checkstyle project(":checkstyle")
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstylePluginVersion}"

    implementation group: 'org.ballerinalang', name: 'ballerina-runtime', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'ballerina-lang', version: "${ballerinaLangVersion}"
    implementation group: 'org.ballerinalang', name: 'value', version: "${ballerinaLangVersion}"
    implementation(group: 'com.solacesystems', name: 'sol-jms', version: "${solaceJmsVersion}") {
        exclude group: 'org.apache.geronimo.specs', module: 'geronimo-jms_2.0_spec'
    }
    implementation group: 'com.solace', name: 'solace-serdes', version: "${solaceSerdesVersion}"
    implementation group: 'javax.jms', name: 'javax.jms-api', version: "${jmsApiVersion}"
    implementation group: 'commons-logging', name: 'commons-logging', version: "${commonsLoggingVersion}"
    implementation group: 'org.apache.servicemix.bundles', name: 'org.apache.servicemix.bundles.jzlib', version: "${servicemixJzlibVersion}"
    implementation group: 'org.osgi', name: 'org.osgi.annotation', version: "${osgiAnnotationVersion}"

    implementation group: 'io.netty', name: 'netty-transport', version: "${nettyVersion}"
    implementation group: 'io.netty', name: 'netty-handler', version: "${nettyVersion}"
    implementation group: 'io.netty', name: 'netty-codec', version: "${nettyVersion}"
    implementation group: 'io.netty', name: 'netty-buffer', version: "${nettyVersion}"
    implementation group: 'io.netty', name: 'netty-common', version: "${nettyVersion}"
    implementation group: 'io.netty', name: 'netty-resolver', version: "${nettyVersion}"
    implementation group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: "${nettyTcnativeBoringsslStaticVersion}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    testLogging {
        showStackTraces = true
        showStandardStreams = true
        events "failed"
        exceptionFormat "full"
    }
    jacoco {
        enabled = true
        destinationFile = file("$buildDir/coverage-reports/jacoco.exec")
        includeNoLocationClasses = true
    }
}

spotbugsMain {
    def classLoader = plugins["com.github.spotbugs"].class.classLoader
    def SpotBugsConfidence = classLoader.findLoadedClass("com.github.spotbugs.snom.Confidence")
    def SpotBugsEffort = classLoader.findLoadedClass("com.github.spotbugs.snom.Effort")
    effort = SpotBugsEffort.MAX
    reportLevel = SpotBugsConfidence.LOW
    reportsDir = file("$project.buildDir/reports/spotbugs")
    def excludeFile = file("spotbugs-exclude.xml")
    if (excludeFile.exists()) {
        it.excludeFilter = excludeFile
    }
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugsTest {
    enabled = false
}

tasks.withType(Checkstyle) {
    exclude '**/module-info.java'
}

checkstyle {
    toolVersion "${checkstylePluginVersion}"
    configFile file("${rootDir}/build-config/checkstyle/build/checkstyle.xml")
    configProperties = ["suppressionFile": file("${rootDir}/build-config/checkstyle/build/suppressions.xml")]
}

checkstyleMain.dependsOn ':checkstyle:downloadCheckstyleRuleFiles'

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn configurations.dist
    from { configurations.dist.collect { it.isDirectory() ? it : zipTree(it) } } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}
